plugins {
    id 'idea'
    id 'java-library'
    id 'maven-publish'
    id 'org.jmailen.kotlinter' version '1.20.1'
    id 'pl.allegro.tech.build.axion-release' version '1.10.0'
    id "com.github.ben-manes.versions" version "0.20.0"
    id "io.spring.dependency-management" version "1.0.6.RELEASE"
    id 'org.jetbrains.kotlin.jvm' version '1.3.10'
    id "org.jetbrains.kotlin.plugin.spring" version '1.3.10'
    id 'org.jetbrains.kotlin.plugin.jpa' version '1.3.10'
    id 'com.github.fhermansson.assertj-generator' version '1.1.2'
}

group = 'se.svt.videocore.lib'
version scmVersion.version

sourceCompatibility = 1.8

compileKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "1.8"
    }
}

compileTestKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "1.8"
    }
}

assertjGenerator {
    classOrPackageNames = ['se.svt.videocore.redisson.config']
    entryPointPackage = 'se.svt.videocore.redisson'
    entryPointTypes = ['STANDARD', 'SOFT']
}

test {
    useJUnitPlatform()
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourceJar {
                classifier "sources"
            }

            repositories {
                maven {
                    ext {
                        if (project.version.endsWith('-SNAPSHOT')) {
                            url "http://svtrepo.jfrog.io/svtrepo/libs-snapshots-local"
                        } else {
                            url "http://svtrepo.jfrog.io/svtrepo/libs-releases-local"
                        }

                        credentials {
                            username 'deployer'
                            password artifactoryDeployPassword
                        }
                    }
                }
            }
        }
    }
}

publish.dependsOn check

ext {}

repositories {
    jcenter()
    mavenCentral()
    maven {
            url "https://svtrepo.jfrog.io/svtrepo/libs-releases"
            credentials {
                username 'reader'
                password artifactoryReaderPassword
            }
        }
}

dependencies {
    implementation 'io.github.microutils:kotlin-logging:1.6.20'
    implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3.10")
    implementation("org.jetbrains.kotlin:kotlin-reflect:1.3.10")
    implementation 'org.springframework.boot:spring-boot:2.1.0.RELEASE'
    implementation 'org.springframework.boot:spring-boot-autoconfigure:2.1.0.RELEASE'
    api 'org.redisson:redisson:3.9.1'
    testImplementation("org.assertj:assertj-core:3.11.1")
    testImplementation 'org.springframework.boot:spring-boot-starter-test:2.1.0.RELEASE'
    testImplementation 'se.svt.videocore.util:junit5-redis-extension:1.0.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
    testImplementation 'io.mockk:mockk:1.8.13'
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
    gradleVersion = '4.10.2'
}